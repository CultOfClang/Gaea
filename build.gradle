import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.70'
    id 'maven-publish'
    id "org.jetbrains.kotlin.plugin.serialization" version '1.3.70'
}

if (System.getenv("GITHUB_RUN_NUMBER")!=null) {
    pluginVersion += "." + System.getenv("GITHUB_RUN_NUMBER")
}

group = pluginGroup
version = pluginVersion

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }

    maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/CultOfClang/Minecraft/")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
            password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
        }
    }
}

dependencies {
    //for some reason this needs to be included
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    compileOnly 'org.bukkit:bukkit:1.15.2-R0.1-SNAPSHOT'
    // i guess.
    implementation 'org.cultofclang.minecraft:kotlinspice:0.1.7'

    //I want to fix this so bad. Not sure why it's required.
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.20.0"
    implementation "org.jetbrains.exposed:exposed-core:0.22.1"
    implementation "org.jetbrains.exposed:exposed-dao:0.22.1"
    implementation "org.jetbrains.exposed:exposed-jdbc:0.22.1"
    implementation "org.xerial:sqlite-jdbc:3.30.1"
    implementation "org.slf4j:slf4j-api:1.7.30"

    //system

    implementation 'com.sksamuel.hoplite:hoplite-core:1.+'
    implementation 'com.sksamuel.hoplite:hoplite-yaml:1.+'

    testImplementation("org.junit.jupiter:junit-jupiter-api:5.6.0")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.6.0")
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/CultOfClang/Minecraft/")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

//Move into plugins folder
if (project.hasProperty("plugin_path") && plugin_path) {
    task copyJar(type: Copy) {
        from jar
        into plugin_path
    }
    build.dependsOn copyJar
}

test{
    useJUnitPlatform()
    testLogging.showStandardStreams = true
    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }
    onOutput { descriptor, event ->
        logger.lifecycle("Test: " + descriptor + " produced standard out/err: " + event.message )
    }
}